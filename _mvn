#compdef mvn

local curcontext="$curcontext" state line cmds ret=1

_arguments -C \
  '(-am --also-make)'{-am,--also-make}'[If project list is specified, also build projects required by the list]' \
  '(-amd --also-make-dependents)'{-amd,--also-make-dependents}'[If project list is specified, also build projects that depend on projects on the list]' \
  '(-B --batch-mode)'{-B,--batch-mode}'[Run in non-interactive (batch) mode]' \
  '(-C --strict-checksums)'{-C,--strict-checksums}'[Fail the build if checksums don'\''t match]' \
  '(-c --lax-checksums)'{-c,--lax-checksums}'[Warn if checksums don'\''t match]' \
  '(-cpu --check-plugin-updates)'{-cpu,--check-plugin-updates}'[Ineffective, only kept for backward compatibility]' \
  '(*)'{-D,--define}'[Define a system property]' \
  '(-e --errors)'{-e,--errors}'[Produce execution error messages]' \
  '(- 1 *)'{-emp,--encrypt-master-password}'[Encrypt master security password]' \
  '(- 1 *)'{-ep,--encrypt-password}'[Encrypt server password]' \
  '(-f --file)'{-f,--file}'[Force the use of an alternate POM file.]:file:->file' \
  '(-fae --fail-at-end)'{-fae,--fail-at-end}'[Only fail the build afterwards; allow all non-impacted builds to continue]' \
  '(-ff --fail-fast)'{-ff,--fail-fast}'[Stop at first failure in reactorized builds]' \
  '(-fn --fail-never)'{-fn,--fail-never}'[NEVER fail the build, regardless of project result]' \
  '(-gs --global-settings)'{-gs,--global-settings}'[Alternate path for the global settings file]:global-settings:->global-settings' \
  '(- 1 *)'{-h,--help}'[Display help information]' \
  '(-l --log-file)'{-l,--log-file}'[Log file to where all build output will go.]:log-file:->log-file' \
  '(-N --non-recursive)'{-N,--non-recursive}'[Do not recurse into sub-projects]' \
  '(-npr --no-plugin-registry)'{-npr,--no-plugin-registry}'[Ineffective, only kept for backward compatibility]' \
  '(-npu --no-plugin-updates)'{-npu,--no-plugin-updates}'[Ineffective, only kept for backward compatibility]' \
  '(-nsu --no-snapshot-updates)'{-nsu,--no-snapshot-updates}'[Supress SNAPSHOT updates]' \
  '(-o --offline)'{-o,--offline}'[Work offline]' \
  '(-P --activate-profiles)'{-P,--activate-profiles}'[Comma-delimited list of profiles to activate]' \
  '(-pl --projects)'{-pl,--projects}'[Build specified reactor projects instead of all projects. A project can be specified by '\['groupId'\]':artifactId or by its relative path.]' \
  '(-q --quiet)'{-q,--quiet}'[Quiet output - only show errors]' \
  '(-rf --resume-from)'{-rf,--resume-from}'[Resume reactor from specified project]' \
  '(-s --settings)'{-s,--settings}'[Alternate path for the user settings file]:settings:->settings' \
  '(-T --threads)'{-T,--threads}'[Thread count, for instance 2.0C where C is core multiplied]' \
  '(-t --toolchains)'{-t,--toolchains}'[Alternate path for the user toolchains file]:toolchains:->toolchains' \
  '(-U --update-snapshots)'{-U,--update-snapshots}'[Forces a check for updated releases and snapshots on remote repositories]' \
  '(-up --update-plugins)'{-up,--update-plugins}'[Ineffective, only kept for backward compatibility]' \
  '(-V --show-version)'{-V,--show-version}'[Display version information WITHOUT stopping build]' \
  '(- 1 *)'{-v,--version}'[Display version information]' \
  '(-X --debug)'{-X,--debug}'[Produce execution debug output]' \
  && ret=0

case "$state" in
  file)
    _wanted files expl file _files -g '*pom*\.xml*' && ret=0
  ;;
  settings)
    _wanted settings expl settings _files -g '*settings*\.xml*' && ret=0
  ;;
  global-settings)
    _wanted global-settings expl global-settings _files -g '*settings*\.xml*' && ret=0
  ;;
  log-file)
    _wanted log-files expl log-file _files && ret=0
  ;;
  toolchains)
    _wanted toolchains expl toolchains _files && ret=0
  ;;
esac
